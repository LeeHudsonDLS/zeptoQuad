

CLOSE

;################################################
;; \file
;; Define motion for 2 slit Apertures
;;
;; Original Author: Tom Cobb
;;
;; Defined axes:
;; - X (addr 6) = centre in same EGUs as aperture
;; - Y (addr 7) = gap in same EGUs as aperture
;;
;; Macros
;; - COORD = 2  CS number, e.g. 2
;; - PLC = 17  PLC number, should be CS number+15, e.g. 17
;; - AP = 1  Axisnum for Aperture+ (clips the +ve edge of the beam), e.g. 1
;; - AM = 2  Axisnum for Aperture- (clips the -ve edge of the beam), e.g. 3
;; - ASIZE = 0  Size of Aperture, e.g. 5.85
;################################################

; Change to CS2
&2

; Set relevant axes to use kinematics
#1->I ; +ve Aperture
#2->I ; -ve Aperture

; These are set by motor_in_cs.template
#define APMOVE P(4700+1)
#define APMRES P(4800+1)
#define APOFF  P(4900+1)
#define AMMOVE P(4700+2)
#define AMMRES P(4800+2)
#define AMOFF  P(4900+2)

; DT sf 25000stps/mm
; Only needed if EPICS isn't used
APMRES = 0.00004
AMMRES = 0.00004
APOFF = 90
AMOFF = -90


; Store aperture size
#define ASIZE Q20
ASIZE = 0

; Calculate gap and centre from aperture positions
#define APPOS (APMRES*P1+APOFF)
#define AMPOS (AMMRES*P2+AMOFF)
#define CENTRE Q7
#define GAP Q8
OPEN FORWARD
CLEAR
    CENTRE = (APPOS + AMPOS)/2
    GAP = ASIZE - AMPOS + APPOS
CLOSE

; Calculate aperture positions in cts from gap and centre
#define APPOS Q228
#define AMPOS Q229
OPEN INVERSE
CLEAR
    ; calculate in EGUs
    APPOS = CENTRE - (ASIZE - GAP)/2
    AMPOS = CENTRE + (ASIZE - GAP)/2
    ; then in cts
    P1 = (APPOS - APOFF)/APMRES
    P2 = (AMPOS - AMOFF)/AMMRES
CLOSE

; A PLC(sx+15) needs to be made to do position reporting
; Readbacks should be in &{axisnum}Q81..89
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define APPOS (APMRES*m162/(I108*32)+APOFF)
#define AMPOS (AMMRES*m262/(I208*32)+AMOFF)
#define CENTRE Q87
#define GAP Q88
OPEN PLC17
CLEAR
    ADDRESS&2
    CENTRE = (APPOS + AMPOS)/2
    GAP = ASIZE - AMPOS + APPOS
    ; If aperture motor record did the last move, set demands = readbacks
    if (APMOVE = 1)
    or (AMMOVE = 1)
        APMOVE = 0
        AMMOVE = 0
        Q77 = Q87
        Q78 = Q88
    endif
CLOSE
ENABLE PLC17
